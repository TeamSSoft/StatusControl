
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Первоначальное заполнение типов
	КоллекцияТипов = Метаданные.ОпределяемыеТипы.tss_sc_ВладелецСтатуса.Тип.Типы();
	Для Каждого ТипВладельца Из КоллекцияТипов Цикл
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипВладельца);
		Если ОбъектМетаданных = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		ПолноеИмя = ОбъектМетаданных.ПолноеИмя();                          
		Если Найти(ПолноеИмя, "Документ.") = 1 Тогда
			Префикс = "Док.: ";
			Значение = Документы[ОбъектМетаданных.Имя].ПустаяСсылка();
		ИначеЕсли Найти(ПолноеИмя, "Справочник.") = 1 Тогда
			Префикс = "Спр.: ";
			Значение = Справочники[ОбъектМетаданных.Имя].ПустаяСсылка();
		Иначе
			Продолжить;
		КонецЕсли;
		СписокВладельцев.Добавить(Значение, Префикс + ОбъектМетаданных.Синоним + " (" + ОбъектМетаданных.Имя + ")");
	КонецЦикла;
	
	Для Каждого Строка Из Объект.ТипыВладельцев Цикл
		ЭлементСписка = СписокВладельцев.НайтиПоЗначению(Строка.ТипВладельца);
		Если ЭлементСписка <> Неопределено Тогда
			ЭлементСписка.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Актуализация таблицы типов
	Объект.ТипыВладельцев.Очистить();
	Для Каждого ЭлементСписка Из СписокВладельцев Цикл
		Если ЭлементСписка.Пометка Тогда
			Объект.ТипыВладельцев.Добавить().ТипВладельца = ЭлементСписка.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
