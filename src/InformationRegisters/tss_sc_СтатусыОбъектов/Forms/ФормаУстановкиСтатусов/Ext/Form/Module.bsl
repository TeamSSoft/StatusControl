&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВладелецСтатуса = Параметры.Отбор.ВладелецСтатуса;
	ИнициализироватьФорму();
	
КонецПроцедуры 

&НаСервере
Процедура ПрименитьИзмененияНаСервере()
	
	Для каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		ВидСтатуса = КлючИЗначение.Значение;
		ЗначениеСтатуса = ЭтаФорма[КлючИЗначение.Ключ];
		tss_sc_УправлениеСтатусами.УстановитьСтатус(ВладелецСтатуса, ВидСтатуса, ЗначениеСтатуса);
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	
	ПрименитьИзмененияНаСервере();
	Модифицированность = Ложь;
	
КонецПроцедуры

// Создание реквизитов и элементов формы
//
Процедура ИнициализироватьФорму()

	СоответствиеСтатусов = tss_sc_УправлениеСтатусами.ПолучитьСтатусыОбъекта(ВладелецСтатуса);
	СтруктураРеквизитов = Новый Структура;
	
	// Новые реквизиты
	ДобавляемыеРеквизиты = Новый Массив;
	Для каждого КлючИЗначение Из СоответствиеСтатусов Цикл
		ВидСтатуса = КлючИЗначение.Ключ;
		ЗначениеСтатуса = КлючИЗначение.Значение;
		ИмяРеквизита = "ВидСтатуса_" + СтрЗаменить(ВидСтатуса.УникальныйИдентификатор(), "-", "_");
		Реквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("СправочникСсылка.tss_sc_ЗначенияСтатусов"),, ВидСтатуса, Истина);
		СтруктураРеквизитов.Вставить(ИмяРеквизита, ВидСтатуса);
		ДобавляемыеРеквизиты.Добавить(Реквизит);
	КонецЦикла; 
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// Новый элемент формы
	Для каждого Реквизит Из ДобавляемыеРеквизиты Цикл
		ВидСтатуса = СтруктураРеквизитов[Реквизит.Имя];
		ЭтаФорма[Реквизит.Имя] = СоответствиеСтатусов[ВидСтатуса];
		
		НовыйМассив = Новый Массив;
		НовыйМассив.Добавить(ВидСтатуса);
		НовыеЗначения = Новый ФиксированныйМассив(НовыйМассив);
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Владелец", НовыеЗначения);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);

		ЭлементФормы = Элементы.Добавить(Реквизит.Имя, Тип("ПолеФормы"), Элементы.ГруппаСтатусов);
		ЭлементФормы.ПутьКДанным = Реквизит.Имя;
		ЭлементФормы.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементФормы.ТолькоПросмотр = Ложь;
		ЭлементФормы.ПараметрыВыбора = ПараметрыВыбора;
		ЭлементФормы.БыстрыйВыбор = Истина;
	КонецЦикла;

КонецПроцедуры // ИнициализироватьФорму()